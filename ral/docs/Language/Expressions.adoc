### Expressions

Expressions are how values to be read *or written* are expressed.

RAL uses tuples as expressions for versatility, though it does not have a tuple type.

__Expr__:

image::images/diagram/Expr.png[]

__ExprAtom__:

image::images/diagram/ExprAtom.png[]

#### Constants

Constants are the simplest kind of expression.

There are four types of constant in RAL.

* Integer (`int` type): `1234`
* Floats (`float` type): `1234.5`
* Strings: `"Hello!"`
* Byte-strings: `[1, 2, 3]` (These values cannot be stored, and are only for use with commands such as `anim`.)


You might be tempted to count `null`, but that's internally an alias.

#### Variable IDs

Variable IDs represent variables or expressions declared or aliased by `let` and `alias`, alongside declared constants.

If a conflict occurs between a declared constant and a local variable, the local variable wins. (This behaviour used to be different, but it became a problem when lambda support stuff folded the callable namespace into the constant namespace and `anim` suddenly became a constant.)

```
let meh = 12;
let result = meh + 8; // 20
```

#### Brackets and Binary Operators

Binary (as in two-parameter) operators are used to perform simple operations on values such as numbers and strings.

The mathematical binary operators are `+` (add/string concatenate), `-` (subtract), `/` (divide), `*` (multiply).

Modulo in CAOS has specific requirements and thus in RAL has been restrained to a macro.

The boolean binary operators are `&&` (logical AND), and `||` (logical OR).

The bitwise binary operators are `&` (bitwise AND), and `|` (bitwise OR).

The comparison operators are `>=` (above or equal), `<=` (below or equal), `>` (above), `<` (below), `!=` (not equal), and `==` (equal).

As for brackets, in terms of precedence (the structure when multiple operators are used in a row), anything within `()` counts as a single "atom" like an ID or a constant.

Outside of that, binary operators separate the entire expression in a given order depending on their precedence. The precedence order (order in which the expression is cut up by binary operators) is:

* `,`: Merges expressions into a group (see below).
* `||`: Logical OR.
* `&&`: Logical AND.
* `==`, `!=`, `<=`, `>=`, `<`, `>`: Comparison operators.
* `+`, `-`: Addition (or string concatenation) / subtraction.
* `/`, `*`: Division and multiplication.
* `|`, `&`: Bitwise OR/AND.

If multiple operators from the same group are used in a row, then the right-most in the expression is chosen.

```
let result1 = (1 + 2) * (3 + 4);   // 21
let result2 = 1 + 2 * 3 + 4;       // 12
let result3 = (((1 - 1) - 1) - 1); // -2
```

##### The Secret Operator: `,`

`,` is an operator which merges expressions together into a group. This is what creates the tuples, and how macros have more than one parameter.

#### Unary Operators

Unary operators are handled when they are found at the start of an expression.

The three unary operators are:

* `!`: Logical NOT
* `~`: Bitwise NOT
* `-`: Negate

```
let a = 1;
let va = -a; // -1
let vb = ~a; // -2
let vc = !a; // 0
```

#### Increment/Decrement Expressions

Appending or prepending `++` or `--` to a variable (or something writable in general) causes an increment or decrement to the value.

If the increment/decrement is appended, then the returned value is that from before the change occurred.

If it is prepended, then the returned value is from after the change occurred.

```
let a = 0;
let b = a++; // b is 0, a is now 1
let c = ++a; // c is 2, a is now 2
let d = a--; // d is 2, a is now 1
let e = --a; // e is 0, a is now 0
```

#### String Embedding

Strings surrounded by `'` instead of `"` allow for "embedding". In these strings, sections surrounded by `{}` are expressions - the values of which are inserted into the string.

```
outs('The hand is named {hand()}.'); // The hand is named Lambert.
```

#### instanceof

`instanceof` checks if a given value matches a given classifier (returning false if the value is null).

It will only perform a null check if the value is not nullable, but will otherwise check all fields (to prevent creating rather brittle code).

```
let isDolphin = targ instanceof Dolphin;
```

#### Message/Script IDs

Message/script ID expressions are used when other methods of writing message and script IDs do not apply.

They consist of the type name, a separator (`:` for scripts, `->` for messages), and the name of the message or script.

```
scrx(Dolphin, Dolphin:chirp);
```

#### Field Accesses

Field accesses take the form of a suffix `.` followed by the ID of a field on the given agent.

They allow accessing fields on agents.

```
field bool Kitten.meowing 1; // meowing is mv01 of Kitten

// ... in some script somewhere ...

kitten.meowing = true;
```

#### Macro/Lambda Calls

Calls act as function calls might in another language.

They may return multiple values, or may return a single value, or may return no values at all.

They may also be passed a varying amount of values, but this is dependent on the specific macro's requirements. See details on macro declarations for more information.

```
myMacro();
let a, b = myOtherMacro(c, d);
```

Notably, macros are considered to be constants of the hidden "any lambda" type.

While that type cannot be passed as arguments (to allow tooling to exist and work properly, and to prevent caller-dependent compile errors, the compiler requires all expression return types are known independent of the macro), regular lambda types can be passed as inline arguments, and can then be called.

This can also be used to define "local functions" (similar to `subr`'s use in CAOS).

See the notes on lambdas for details.

#### Inline Expressions

Inline expressions are `@` followed by the syntax for a string embedding.
However, rather than this being a real string embedding, variables are instead replaced with references to them in CAOS.

Something of a hiccup is that the type is `any` - explicit casts are particularly useful for this.

```
let var = "important"; // doesn't need to be this convoluted, but makes a good example
let important = @'game {var}'!float;
```

##### Inline Variables

Inline variables are like inline expressions, except rather than `@`, `@=` is used.

The main distinction is that they are expected to be CAOS variables rather than CAOS expressions, and therefore are writable by the CAOS `sets`/`setv`/`seta` commands.

They're also still readable, just like inline expressions.

```
@= 'game "moose"' = 12; // setv game "moose" 12
```

#### Statement Expressions

Statement expressions are expressions of statements.

Like blocks, their syntax is to surround the statements with `{}`.

However, unlike blocks, at their very end a `return ...;` statement may be provided containing the expressions to return to the caller. (If no `return ...;` statement is provided, then the expression returns nothing.)

Macros typically use these and don't show it.

```
let result = {let a = 1; let b = 2; return a + b;};
```

#### Lambda Expressions

Lambda expressions are expressions which define macros as compile-time values.

These compile-time values can be passed inline into other macros, which can then call them.

```
macro () a(lambda(str name)(str @last) @b, str @last) {
 outs("hi ");
 outs(b(last));
 outs("\n");
}

install {
 a(lambda (str @last) {
  return "jeremy " + last;
 }, "woof");
}
```

Note that the data that makes up a lambda is deliberately removed from the value if it is passed around in any way that might allow it to escape a scope (which would allow access to out-of-scope variables, which could overlap with newer variables).

#### Explicit Casts

Explicit casts, written as `!` followed by something that is not an ID (indicates cast to non-nullable) or by a type (indicates cast to that type), mostly ignores the existing type of the value in favour of an overridden one.
This works both ways and may be used to cast a variable you are going to write into.

This is particularly important for working with inline statements and expressions.

```
let modu = @'modu'!str;
```

##### Initial Scope

The initial scope is the set of variables available in every script.

Two things are of note here: first, that these variables may be retyped with `alias` and casting, and second, that macros do not inherit the types from the caller (this is because otherwise errors, and therefore diagnostics, are not consistent between callers).

* `ownr` - Type is usually derived from the classifier of this script, except where `overrideOwnr` intervenes. For macros, this is `Agent?`.
* `from` - Type is assumed to be `any`, usually, except `overrideOwnr` changes that. For macros, this is again `any`.
* `_p1_` - Type is `any`.
* `_p2_` - Type is `any`.
* `null` - Type is `null`.
* `targ` - Type is `Agent?`. Many commands use this to select the target agent.

