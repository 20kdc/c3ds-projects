// ovrv

Type ::= ID "?"? ("|" ID "?"?)*

// decl

Decl ::=
	IncludeDecl |
	AddSearchPathDecl |

	TypedefDecl |
	ClassDecl |
	InterfaceDecl |
	FieldDecl |
	MessageDecl |
	ScriptProtoDecl |
	OverrideOwnrDecl |
	MessageHookDecl |
	AssertConstDecl |
	ConstantDecl |

	ScriptCodeDecl |
	InstallDecl |
	RemoveDecl |
	MacroDecl

IncludeDecl ::= "include" ConstStringExpr ";"
AddSearchPathDecl ::= "addSearchPath" ConstStringExpr ";"
CodeGenFeatureLevelDecl ::= "codeGenFeatureLevel" ("c3" | "ds" | "customEngine") ";"

TypedefDecl ::= "typedef" ID Type ";"
ClassDecl ::= "class" ID (ConstIntExpr ConstIntExpr ConstIntExpr)? ("extends" ID)* ";"
InterfaceDecl ::= "interface" ID ("extends" ID)* ";"
FieldDecl ::= "field" Type ID "." ID ConstIntExpr ";"
MessageDecl ::= "message" ID ("->" | ":") ID ConstIntExpr ";"
ScriptProtoDecl ::= "script" ID ("->" | ":") ID ConstIntExpr ";"
OverrideOwnrDecl ::= "overrideOwnr" ConstIntExpr Type ";"
MessageHookDecl ::= "messageHook" ConstIntExpr ";"
AssertConstDecl ::= "assertConst" ConstIntExpr ";"
ConstantDecl ::= ID "=" ConstExpr ";"

ScriptCodeDecl ::= "script" ID (("->" | ":") ID | ConstIntExpr) Stmt
InstallDecl ::= "install" Stmt
RemoveDecl ::= "remove" Stmt
MacroDecl ::= "macro" (ArgList ID ArgList Stmt | ID ArgList Expr)
MacroArgList ::= "(" MacroArg ("," MacroArg)* ")"
MacroArg ::= Type ("@" | "@=")? ID

// stmt

Stmt ::=
	EmptyStmt |
	BlockStmt |
	LetStmt |
	AliasStmt |

	InlineStmt |
	AssignStmt |
	ModifyStmt |
	EmitStmt |
	CallStmt |

	IfStmt |
	WhileStmt |
	LoopStmt |
	ForStmt |
	BreakStmt |
	ContinueStmt |
	ForeachStmt |
	WithStmt

EmptyStmt ::= ";"
BlockStmt ::= "{" Stmt* "}"
LetStmt ::= "let" LetStmtBody ";"
LetStmtBody ::= Type? ID ("," Type? ID)* ("=" Expr)? ";"
AliasStmt ::= "alias" ID ("=" Expr | "!" Type) ";"

InlineStmt ::= "@" StringEmbed+ ";"
AssignStmt ::= (Expr "=")? Expr ";"
ModifyStmt ::= Expr ("+=" | "-=" | "*=" | "/=" | "|=" | "&=") Expr ";"
EmitStmt ::= Expr "->" ID "(" Expr ")" ("after" Expr)? ";"
CallStmt ::= "call" ID "(" Expr ")" ";"

IfStmt ::= "if" Expr Stmt ("else" Stmt)?
WhileStmt ::= "while" Expr Stmt
LoopStmt ::= "loop" Stmt
ForStmt ::= "for" LetStmtBody ";" Expr ";" Stmt Stmt
BreakStmt ::= ("break" | "continue") ";"
ForeachStmt ::= "foreach" "(" Type "in" ID Expr? ")" Stmt
WithStmt ::= "with" ("(" Type ID ")" | Type ID) Stmt ("else" Stmt)?

// expr

Expr ::=
	ExprAtom
	(
		("," | "||" | "&&" | "==" | "!=" | "<=" | ">=" | "<" | ">" | "+" | "-" | "/" | "*" | "|" | "&")
		ExprAtom
	)*

ExprAtom ::=
	// parseExprAtomOrNull
	(
		INT |
		STRING |
		FLOAT |
		ID |
		StringEmbed |
		"@" StringEmbed |
		"@=" StringEmbed |
		("{" Stmt* ("}" | ("return" Expr? ";" "}"))) |
		"(" Expr? ")" |
		("++" | "--" | "!" | "~" | "-") ExprAtom |
		"[" ConstByteListExpr "]"
	)
	// parseExprSuffix
	(
		"instanceof" Type |
		("++" | "--") |
		// this is actually call syntax
		"(" Expr? ")" |
		// this is actually message ID syntax
		(":" | "->") ID |
		"." ID |
		"!" Type?
	)*

