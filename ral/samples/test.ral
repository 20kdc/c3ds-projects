/*
 * c3ds-projects - Assorted compatibility fixes & useful tidbits
 * Written starting in 2022 by contributors (see CREDITS.txt)
 * To the extent possible under law, the author(s) have dedicated all copyright and related and neighboring rights to this software to the public domain worldwide. This software is distributed without any warranty.
 * You should have received a copy of the CC0 Public Domain Dedication along with this software. If not, see <http://creativecommons.org/publicdomain/zero/1.0/>.
 */

// Rational (as opposed to CAOS) Agent Language
// Sample/Compiler Test Sheet

// Consts can be defined as so:
creatureGenus = 4;

/* Some expressions can be calculated as consts! */
aFalse = !(1 || 0);

assertConst aFalse == 0;

aTrue = !aFalse;

// Files can be included.
// Where constants are expected, defined constants are usable.
// In practice, you are expected to use include like C++ headers.

fileToIncludeFrom = "test-include.ral";
include fileToIncludeFrom;

// include search paths can be added
// addSearchPath ".";

// Interfaces represent shared APIs between agents.
// They may also be declared as extending other interfaces (or classes).

interface SomethingCreaturesHave;

// Classes are interfaces with a place in the Scriptorium.
// Classes automatically extend other classes based on their classifier.

class SystemAgent 1 0 0;
class WorldAgent 2 0 0;
class Creature creatureGenus 0 0 extends SomethingCreaturesHave;

// Typedefs and TypeScript-style union types exist.

typedef number int|float;

// Messages may be defined on interfaces.
// Message numbers so defined are available as constants.

message Agent:windowResized 123;

// Macros are used to allow for global subroutines without message passing.
// Macro parameters may be marked as & (inline).
// These parameters are re-evaluated on each use.
// Macro parameters not so marked will be copied into local variables.

macro outs(str &text) {
	// The & command is used to write & CAOS.
	// Parameters given are converted into CAOS that returns their values.
	// When possible, this CAOS can be used to write to the value.
	& 'outs {text}\n';
	// It's worth mentioning that this style of macro is considered an expression.
	// {} is valid in expressions, although it has different semantics to
	//  when used as a statement.
	// In particular, it may have a return statement at the end.
}

macro modu() & "modu"!str;

// In any case, inline parameters can alter values.
// So it comes down to desired style.

macro addInplace(int &a, int &b) {
	// Assignment works as in most languages, more or less.
	// However, you may assign multiple source values to multiple destinations.
	// However, due to various design concerns, these assignments are not atomic.
	// Therefore a,b = b,a; is not recommended.
	& 'addv {a} {b}\n';
}

// And, of course, at the expense of a temporary variable...
// It is possible to do things the simple way.

macro add(int &a, int &b) {
	let int c = a;
	& 'addv {c} {b}\n';
	return c;
}

// There is a second style of macros.
// This style is useful for when a macro needs to modify variables in-place for efficiency.
// In this style, the "return values" are given as explicit slots.
// Such a macro is a statement or list of statements, not an expression.
// It returns it's values through it's side effects.

// Null is considered a separate type.
// ? acts as syntactic sugar for allowing null in a type.

macro (Agent? res) rtar(int &family, int &genus, int &species) {
	& 'rtar {family} {genus} {species}\n';
	res = targ;
}

// The install script is simply install followed by a statement block.

install {

	& ' * --outs/modu--\n';

	// Macro calls are relatively normal function if you're used to other languages.
	outs("Your MODU says:"); outs(modu()); outs("\n");

	& ' * --defining counter--\n';

	// Variables may be defined using the let keyword.
	let number counter = 0;

	// They may be aliased using the alias keyword, and this also allows redeclaring their type.
	alias counter = counter!int;

	& ' * --addInplace--\n';
	addInplace(counter, 1);

	& ' * --add--\n';
	counter = add(counter, 1);

	// bools are strictly speaking ints (and are castable to such).
	// But they're useful nonetheless.
	let bool testBool = counter == 2 && counter < 4 || counter > 3;
	// And of course they can be used in conditionals.
	if testBool && counter == 1 {
		outs("Moo!");
	} else if counter == 2 {
		outs("Blep!");
	} else {
		outs("Meh!");
	}

	// RAL also has sane loops, at least where possible...
	while counter == 4 {
	}

	// Agent classifiers expand to their components.
	// THIS DOESN'T WORK YET
	//let WorldAgent? y = rtar(WorldAgent)!WorldAgent?;
	let WorldAgent? y = rtar(0, 0, 0)!WorldAgent?;
}

// Scripts can be defined by class name and message ID...

script Creature 5 {
	// Constant expressions can be written inline...
	& 'outv {aTrue}\n';
	// And constant expressions can cause code to be elided at compile-time
	if aFalse {
		& 'outs "Invisible"\n';
	} else {
		& 'outs "Visible"\n';
	}
	// test of lexer
	& 'outv {-0.1}';
	& 'outv {+0.1}';
	& 'outv {-0}';
}

// ...or by message name.

message Creature:exampleTypeSpecific 8000;
field str Creature.someString 1;
field Creature Creature.someRef 2;

script Creature:windowResized {
	// emit
	ownr->exampleTypeSpecific("A", "B") after 3;
	ownr->exampleTypeSpecific("A") after 0;
	ownr->exampleTypeSpecific() after 0;
	ownr->exampleTypeSpecific("A", "B");
	ownr->exampleTypeSpecific("A");
	ownr->exampleTypeSpecific();
	// fields
	ownr.someString = "Wowzers";
	ownr.someRef.someString = "Indirection!";
	ownr->exampleTypeSpecific(ownr.someRef.someString);
	// test this!
	targ = ownr.someRef;
	// test enum stuff
	foreach (Creature in econ ownr) {
		targ.someString = "econ";
	}
	foreach (Creature in enum) {
		targ.someString = "enum";
	}
	foreach (Creature in epas) {
		targ.someString = "epas";
	}
	foreach (Creature in esee) {
		targ.someString = "esee";
	}
	foreach (Creature in etch) {
		targ.someString = "etch";
	}
	// test break
	foreach (Creature in econ ownr) {
		if targ.someString == "break" {
			break;
		}
	}
}

// The remove script is handled similarly to the install script.

remove {
	let float test = 12.5;
	let float test2 = 12.5;
	let float test3 = 12.5;
	let int testInt = 12;
	let int testInt2 = 12;
	let int testInt3 = 12;
	let float testAllOps = test + test2 * test3 / test - (testInt | testInt2 & testInt3);
	let float testAllOpsConst = 12.5 + 12.5 * 12.5 / 12.5 - (12 | 12 & 12);
	&'outs {"Hello, test float " + test + " and string concat!\n"}';
	&' * silly commented scriptorium value test {Creature}';
}

