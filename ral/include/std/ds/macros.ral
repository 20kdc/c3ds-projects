/*
 * c3ds-projects - Assorted compatibility fixes & useful tidbits
 * Written starting in 2022 by contributors (see CREDITS.txt)
 * To the extent possible under law, the author(s) have dedicated all copyright and related and neighboring rights to this software to the public domain worldwide. This software is distributed without any warranty.
 * You should have received a copy of the CC0 Public Domain Dedication along with this software. If not, see <http://creativecommons.org/publicdomain/zero/1.0/>.
 */

include "std/ds/classes.ral";

// "DS/NEW portals.cos"

// Is this portal index in use?
macro dsPortalIDInUse(int index) {
	let result = false;
	foreach (DSPortal in enum) {
		if targ.index == index {
			result = true;
			break;
		}
	}
	return result;
};

// Allocates a portal ID. Returns -1 on failure.
macro dsPortalIDAlloc() {
	let id = 9;
	for ; id > -1; id--; {
		if !dsPortalIDInUse(0) {
			break;
		}
	}
	return id;
};

// Wipes settings for the given portal.
macro () dsPortalClear(int @id) {
	let base = '_portal{id}';
	let suffixR = '{base}receive';
	let suffixS = '{base}send';
	let gvar = gamn("");
	while gvar != "" {
		let tvar = gvar;
		// advance early so that removal is harmless (hopefully)
		gvar = gamn(gvar);
		// remove?
		if strEndsWith(tvar, suffixR) || strEndsWith(tvar, suffixS)
			delg(tvar);
	}
}

// Special: Any online user
DS_PORTAL_USER_ANY = "!net: ruso";
// Special: Any friend
DS_PORTAL_USER_FRIEND = "!friend";

// Adds a source for the given portal.
macro () dsPortalAddFrom(int @id, str @user) {
	gameInt('{user}_portal{id}receive') = 1;
}

// Adds a target for the given portal.
macro () dsPortalAddTo(int @id, str @user) {
	gameInt('{user}_portal{id}send') = 1;
}

// "DS/zzz_gamestart_login.cos" and friends

// Attempts to login to the DS servers, assuming `netPass` has been called.
// This function takes care of poking the topleft UI and setting the status.
// Returns true if login was successful.
macro dsTryLogin() {
	netLine(true);
	inst();
	let res = netLine();
	if res {
		gameStr("status") = "online";
		targ = gameAgent("ds_gui_topleft");
		if targ != null {
			part(7);
			pose(1);
		}
	}
	return res;
};

// "DS/new_ds_fav_places.cos"

/*
Allocates a favorite place ID.
*/
macro (int res) dsFavPlacesNewIndex() {
	res = gameInt("ds_favourites");
	gameInt("ds_favourites") += 1;
}
